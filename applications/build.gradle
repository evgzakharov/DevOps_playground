buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

task copyJars(type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$buildDir/allJars"
}

task clean {
    delete file(buildDir)
}

group = 'docker_services'

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'docker'

    sourceCompatibility = 1.8

    task generateSourceFolders {
        doLast {
            sourceSets.all { set ->
                set.allSource.srcDirs.each { it.mkdirs() }
            }
        }
    }

    task buildDocker(type: Docker, dependsOn: build) {
        push = false
        applicationName = jar.baseName
        dockerfile = file('src/main/docker/Dockerfile')
        doFirst {
            copy {
                from jar
                into stageDir
            }
        }
    }
}

